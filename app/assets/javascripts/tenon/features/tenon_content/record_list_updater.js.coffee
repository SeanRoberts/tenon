class Tenon.features.RecordListUpdater
  constructor: ->
    $(document).on('click', 'a.record-list-updater', @updateRecordList)
    $(window).on('popstate', @previousRecords)

  updateRecordList: (e) =>
    e.preventDefault()
    $link = $(e.currentTarget)
    query = URI($link.attr('href')).query()
    hash = URI($link.attr('href')).hash()
    console.log(hash)
    history.pushState({recordListUpdate: true, hash: hash}, $link.textContent, "?#{query}")

    # If there's no hash we reload the records.  If there is a hash, we add it
    # and then the records will be reloaded by the call to window.onpopstate
    # that is generated by changing the hash
    if hash == '' || hash == '#'
      Tenon.activeRecordList.refresh(clear: true)
    else
      $(window).trigger('popstate')
      window.location.hash = hash

  previousRecords: (e) =>
    state = e.originalEvent.state
    if state && state.recordListUpdate
      Tenon.activeRecordList.refresh(clear: true)